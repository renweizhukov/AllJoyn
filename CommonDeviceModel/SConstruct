# Copyright AllSeen Alliance. All rights reserved.
#
#    Permission to use, copy, modify, and/or distribute this software for any
#    purpose with or without fee is hereby granted, provided that the above
#    copyright notice and this permission notice appear in all copies.
#
#    THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
#    WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
#    MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
#    ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
#    WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
#    ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
#    OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
# 

import os
import Utility
    
componentName = 'CommonDeviceModel'

vars = Variables()

def GetPathFromEnvironVarOrArgument(pathEnvName, description, argumentName):
    defaultPath = Utility.GetDefaultPathFromEnvironVar(description, pathEnvName)
    path = ARGUMENTS.get(argumentName, defaultPath)
    if not path:
        Utility.PrintOneLineLog(Utility.EventLevel.error, componentName, 
            'The {0} path is not found.'.format(description))
        Exit(1)
    
    Utility.PrintOneLineLog(Utility.EventLevel.info, componentName,
        'The {0} path is {1}.'.format(description, path))
    
    vars.Add(PathVariable(argumentName, 
        'Directory containing the {0}'.format(description), 
        path))  
    
    return path

# Get the version of AllJoyn core from scons command line arguments and check 
# if it is supported. If AJ_VER is not specified, it will be set to the default 
# value.
defaultAlljoynVersion = '1604'
alljoynVersion = ARGUMENTS.get('AJ_VER', defaultAlljoynVersion)

supportedAlljoynVersions = ['1604']
if alljoynVersion not in supportedAlljoynVersions:
    Utility.PrintOneLineLog(Utility.EventLevel.error, GatewayName, 
    'Building with this alljoyn core version {0} is not supported.'.format(alljoynVersion))
    Exit(1)

vars.Add(EnumVariable('AJ_VER',
                      'AllJoyn core version',
                      alljoynVersion,
                      allowed_values=('1604')))
    
# If AJ_SRC_PATH is not specified, use the OS environment variable to generate 
# the default AllJoyn core source path.
alljoynCoreSrcPathEnvName = 'ALLJOYN_SRC_' + alljoynVersion + '_HOME'
alljoynCoreSrcPath = GetPathFromEnvironVarOrArgument(
                        alljoynCoreSrcPathEnvName, 
                        'AllJoyn core source', 
                        'AJ_SRC_PATH')

# Get the global environment from AllJoyn build_core SConscript.
alljoynBuildCorePath = alljoynCoreSrcPath + '/core/alljoyn/build_core/SConscript'
alljoynBuildCorePath = alljoynBuildCorePath.replace('/', os.sep)
alljoynEnv = SConscript(alljoynBuildCorePath)
Utility.PrintAlljoynEnv(alljoynEnv)

# Clone a copy of AllJoyn core environment since we may need to change some 
# construction variables and want to keep the AllJoyn core environment intact.
env = alljoynEnv.Clone()

# Set additional construction variables which are not included in the AllJoyn 
# core environment.
vars.Update(env)
Help(vars.GenerateHelpText(env))
        
# Check if the specified build arguments are supported. Currently only the 64-bit build on 
# Linux is supported.
if not Utility.IsBuildSupported(alljoynEnv):
    Utility.PrintOneLineLog(Utility.EventLevel.error, GatewayName, 
    'Only the build configuration linux+x86_64 is supported.')
    Exit(1)
    
# Add the common AllJoyn core and CDM include paths.
commonIncludePathList = \
    ['$AJ_SRC_PATH' + Utility.ReplacePathSeparators('/core/alljoyn/common/inc'), 
     '$AJ_SRC_PATH' + Utility.ReplacePathSeparators('/core/alljoyn/alljoyn_core/inc'),
     '$AJ_SRC_PATH' + Utility.ReplacePathSeparators('/core/alljoyn/build/$OS/$CPU/$VARIANT/dist/cpp/inc'),
     '$AJ_SRC_PATH' + Utility.ReplacePathSeparators('/services/cdm/cpp/inc'),
     '#inc', 
     Utility.ReplacePathSeparators('#src/BaseSample'),
     Utility.ReplacePathSeparators('#src/interfaces/vendor_defined')]
env.Append(CPPPATH = commonIncludePathList)

# Add AllJoyn library paths.
alljoynLibraryPathList = \
    ['$AJ_SRC_PATH' + Utility.ReplacePathSeparators('/services/cdm/build/$OS/$CPU/$VARIANT/dist/cpp/lib'),
     '$AJ_SRC_PATH' + Utility.ReplacePathSeparators('/services/cdm/build/$OS/$CPU/$VARIANT/dist/cdm/lib')]
env.Append(LIBPATH = alljoynLibraryPathList)

# Add AllJoyn libraries.
env.Append(LIBS = ['ajrouter', 'alljoyn', 'alljoyn_about', 'alljoyn_cdm'])

# Suppress the unused-parameter warning.
env.Append(CPPFLAGS=['-Wno-unused-parameter'])

# Invoke the SConscript to build the BingoControllee executable.
env.SConscript(['./src/BingoControllee/SConscript'], 
    variant_dir = '$OBJDIR/cdm/BingoControllee', 
    duplicate = 0, 
    exports = ['env'])   

# Invoke the SConscript to build the BingoController executable.  
env.SConscript(['./src/BingoController/SConscript'], 
    variant_dir = '$OBJDIR/cdm/BingoController', 
    duplicate = 0, 
    exports = ['env'])
        
# For Linux, install the AllJoyn shared libraries which are required for running the executable.
if env['OS'] == 'linux':
    alljoynCoreLibPath = alljoynCoreSrcPath + '/services/cdm/build/$OS/$CPU/$VARIANT/dist/cpp/lib/'
    env.Command(target = "$DISTDIR/cdm/bin/liballjoyn.so",
        source = alljoynCoreLibPath + 'liballjoyn.so',
        action = Copy("$TARGET", "$SOURCE"))
    env.Command(target = "$DISTDIR/cdm/bin/liballjoyn_about.so",
        source = alljoynCoreLibPath + 'liballjoyn_about.so',
        action = Copy("$TARGET", "$SOURCE"))
    
    alljoynCdmLibPath = alljoynCoreSrcPath + '/services/cdm/build/$OS/$CPU/$VARIANT/dist/cdm/lib/'
    env.Command(target = "$DISTDIR/cdm/bin/liballjoyn_cdm.so",
        source = alljoynCdmLibPath + 'liballjoyn_cdm.so',
        action = Copy("$TARGET", "$SOURCE"))
